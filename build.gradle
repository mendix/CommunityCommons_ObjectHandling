
// This gradle build depends on Mendix internal resources, the lastest commit
// which did not is 6e1527a5e31347c6914bc34deab31ef4ab46953b. You may restore
// the gradle scripts to that commit if you which to use them at your own risk.
// We don't guarantee that those will keep working in the future.

import com.github.jk1.license.render.*

plugins {
    id 'java'
    id 'com.mendix.gradle.publish-module-plugin' version '1.23'
    id 'net.researchgate.release' version '2.8.1'
    id 'com.github.jk1.dependency-license-report' version '2.0'
}

sourceCompatibility = '21'
[compileJava, compileTestJava]*.options*.encoding = 'UTF-8'

project.ext {
    mendixPublicApiVersion = '10.24.0'
}

def mprDir = "$projectDir/src/ObjectHandling"

mxMarketplace {
    appNumber = 37114
    appName = 'ObjectHandling'
    moduleName = 'ObjectHandling'
    moduleLicense = 'Apache V2'
    appDirectory = "src/ObjectHandling"
    versionPathPrefix = "_Docs/version " // the path prefix within the module to the version folder
    includeFiles = ["$mprDir/License.txt", "$mprDir/SiemensMendixObjectHandling__5.2.0__READMEOSS.html"]
    syncJavaDependenciesBeforeBuild = true
}

def userLibDir = "$mprDir/userlib"
def vendorLibDir = "$mprDir/vendorlib"

repositories {
    maven {
        url 'https://nexus.rnd.mendix.com/repository/repo1-proxy/'
    }
    maven {
        url 'https://nexus.rnd.mendix.com/repository/maven-hosted/'
    }
}

configurations {
    implementation {
        canBeResolved = true
    }
}

dependencies {
    testImplementation(
            [group: 'com.mendix', name: 'public-api', version: "$mendixPublicApiVersion"],
            [group: 'junit', name: 'junit', version: '4.13.1'],
            [group: 'commons-io', name: 'commons-io', version: '2.11.0'],
            [group: 'org.apache.httpcomponents.core5', name: 'httpcore5', version: '5.0.3'],
            [group: 'org.apache.httpcomponents.client5', name: 'httpclient5', version: '5.0.3']
    )

    testImplementation fileTree(vendorLibDir) { include '*.jar' }

    compileOnly([group: 'com.mendix', name: 'public-api', version: "$mendixPublicApiVersion"])
}

tasks.withType(JavaCompile) {
    options.deprecation = true
}

sourceSets {
    main {
        java {
            srcDir '$mprDir/javasource'
            exclude 'unittesting/**'
            exclude 'system/UserActionsRegistrar.java'
        }
    }
    test {
        java {
            srcDirs = ["src/test", "$mprDir/javasource"]
        }
    }
}

test {
    exclude 'unittesting/**'
}

task copyAllToUserlib(type: Copy) {
    group "Build"
    description "Copies all dependencies to the userlib directory."

    from configurations.testCompileClasspath
    into userLibDir
    eachFile { fileCopyDetails -> new File(destinationDir, "${fileCopyDetails.name}.${project.name}.RequiredLib").write '' }
}

clean {
    delete "$userLibDir"
    delete "$vendorLibDir"
}

tasks.named('mxBuild') {
    dependsOn copyAllToUserlib
}

tasks.named('compileJava') {
    dependsOn mxBuild
}

release {
    tagTemplate = '$name-$version'
}

task afterReleaseBuildTask {
    dependsOn 'clean'
    dependsOn 'publishModuleToMarketplace'

    tasks.findByName('publishModuleToMarketplace').mustRunAfter 'clean'
}

afterReleaseBuild.dependsOn afterReleaseBuildTask

licenseReport {
    allowedLicensesFile = new File("$projectDir/allowed-licenses.json")
    renderers = [new TextReportRenderer()]
}
